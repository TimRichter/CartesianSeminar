{-# OPTIONS --without-K #-}

module GibbonsCoPrograms where

-- code "scratchpad" Cartesian Seminar November 2021,
-- reading Gibbons : "How to design Co-Programs"

-------------------
-- natural numbers
-------------------

data â„• : Set where
  Z : â„•
  S : â„• â†’ â„•

-- to enable use of natural number literals, e.g. "0", "1", "156372",...

{-# BUILTIN NATURAL â„• #-} 

------------
-- Lists
------------

infixr 20 _::_

data List (A : Set) : Set where
  []   : List A
  _::_ : A â†’ List A â†’ List A

-------------
-- Booleans
-------------

data ğ”¹ : Set where
  true : ğ”¹
  false : ğ”¹

-- boolean "less or equal" test on â„•

infix 20 _â‰¤?_

_â‰¤?_ : â„• â†’ â„• â†’ ğ”¹
Z â‰¤? m = true
S n â‰¤? Z = false
S n â‰¤? S m = n â‰¤? m

-------------------------
-- (type level) equality
-------------------------

infix 25 _â‰¡_

data _â‰¡_ {A : Set} (a : A) : A â†’ Set where
  Refl : a â‰¡ a

-- (this is just to let Agda know about our definition
-- of equality, so in with clauses we can use "in eq")

{-# BUILTIN EQUALITY _â‰¡_ #-}

-- i.e. for any type |A| and any two elements |a : A| and |b : A|,
-- we have defined the type |a â‰¡ b| (Note that within comments, we enclose
-- code snippets with |...|). The elements of this type are proofs that
-- |a| is equal to |b|. We have only one constructor, |Refl|.
-- It gives us, (for any type |A| and) for any |a : A|, a proof that |a| is
-- equal to itself. E.g. |1 â‰¡ 1| by |Refl|:

lala : (S Z) â‰¡ (S Z)
lala = Refl

-- This (|2 â‰¡ 1|) better should not typecheck (and it doesn't):
-- gugu : S (S Z) â‰¡ S Z
-- gugu = Refl

-- Ok, sounds rather boring so far... it will turn out that it isn't.

-- Here are some properties of equality, all proved by pattern matching
-- using the only constructor, |Refl|. That we are allowed to do this is
-- actually not completely trivial ... compare "path induction" in the
-- HoTT-book!

-- Reflexivity is just proved by |Refl| itself

â‰¡-reflexive : {A : Set} â†’ {a : A} â†’ a â‰¡ a
â‰¡-reflexive = Refl

-- We have Symmetry:

â‰¡-symmetric : {A : Set} â†’ {a b : A} â†’ a â‰¡ b â†’ b â‰¡ a
â‰¡-symmetric Refl = Refl

-- and Transitivity:

â‰¡-transitive : {A : Set} â†’ {a b c : A} â†’ a â‰¡ b â†’ b â‰¡ c â†’ a â‰¡ c
â‰¡-transitive Refl Refl = Refl

-- so, |â‰¡| actually defines an equivalence relation on each type |A|.

-- We can introduce notation for equational reasoning:

_â–¡ : {A : Set} â†’ (a : A) â†’ a â‰¡ a
a â–¡ = Refl {a = a}

infixr 10 _â‰¡âŸ¨_âŸ©_

_â‰¡âŸ¨_âŸ©_ : {A : Set} â†’ {b c : A} â†’
         (a : A) â†’ a â‰¡ b â†’ b â‰¡ c â†’ a â‰¡ c
a â‰¡âŸ¨ p âŸ© q = â‰¡-transitive p q

-- Here are two more interesting properties of |â‰¡| :
-- it is a congruence with respect to an function, that means:

â‰¡-cong : {A B : Set} â†’ {aâ‚ aâ‚‚ : A} â†’ (f : A â†’ B) â†’
         aâ‚ â‰¡ aâ‚‚ â†’ f aâ‚ â‰¡ f aâ‚‚
â‰¡-cong f Refl = Refl

-- If |B| is a type family over a type |A| (e.g. |B a| is some
-- property of the element |a : A|), and |aâ‚ â‰¡ aâ‚‚|, there is
-- a map |B aâ‚ â†’ B aâ‚‚| (e.g. from a proof that aâ‚ has property |B|,
-- one can deduce that aâ‚‚ also has property |B|):

â‰¡-transport : {A : Set} â†’ {B : A â†’ Set} â†’ {aâ‚ aâ‚‚ : A} â†’
              aâ‚ â‰¡ aâ‚‚ â†’ B aâ‚ â†’ B aâ‚‚
â‰¡-transport Refl p = p


--------------------
-- insertion sort
--------------------

-- first for â„• with â‰¤?

insertâ„• : â„• â†’ List â„• â†’ List â„•
insertâ„•  n [] = n :: []
insertâ„•  n (m :: ms) with n â‰¤? m        --  the "with" construct allows pattern matching
... | true  = n :: m :: ms              --  over the result of any expression, here |n â‰¤? m|.
... | false = m :: insertâ„•  n ms        --  The dots (...) abbreviate |insertâ„• n (m :: ms)| here.  

insertSortâ„• : List â„• â†’ List â„•
insertSortâ„•  []        = []
insertSortâ„•  (n :: ns) = insertâ„• n (insertSortâ„• ns)


testproof : insertSortâ„• (3 :: 2 :: 1 :: []) â‰¡ (1 :: 2 :: 3 :: [])
-- We can prove this in equational reasoning style like
-- we did on the blackboard. Note that here the justifications
-- are just comments: all of them are definitions, and Agda knows
-- about these and uses them to do reduction. So the actual proof
-- for each step is just Refl.
testproof =
  insertSortâ„• (3 :: 2 :: 1 :: [])
  â‰¡âŸ¨ Refl {- Def insertSortâ„•, rule #2 -} âŸ©
  insertâ„• 3 (insertSortâ„• (2 :: 1 :: []))
  â‰¡âŸ¨ Refl {- Def insertSortâ„•, rule #2 -} âŸ©
  insertâ„• 3 (insertâ„• 2 (insertSortâ„• (1 :: [])))
  â‰¡âŸ¨ Refl {- Def insertSortâ„•, rule #2 -} âŸ©
  insertâ„• 3 (insertâ„• 2 (insertâ„• 1 (insertSortâ„• [])))
  â‰¡âŸ¨ Refl {- Def insertSortâ„•, rule #1 -} âŸ©
  insertâ„• 3 (insertâ„• 2 (insertâ„• 1 []))
  â‰¡âŸ¨ Refl {- Def insertâ„•, rule #1 -} âŸ©
  insertâ„• 3 (insertâ„• 2 (1 :: []))
  â‰¡âŸ¨ Refl {- Def insertâ„•, rule #2, case false -} âŸ©
  insertâ„• 3 (1 :: (insertâ„• 2 []))
  â‰¡âŸ¨ Refl {- Def insertâ„•, rule #2, case false -} âŸ©
  (1 :: insertâ„• 3 (insertâ„• 2 []))
  â‰¡âŸ¨ Refl {- Def insertâ„•, rule #1 -} âŸ©
  (1 :: insertâ„• 3 (2 :: []))
  â‰¡âŸ¨ Refl {- Def insertâ„•, rule #2, case false -} âŸ©
  (1 :: 2 :: (insertâ„• 3 []))
  â‰¡âŸ¨ Refl {- Def insertâ„•, rule #1 -} âŸ©
  (1 :: 2 :: 3 :: [])
  â–¡
-- And since
--   |â‰¡-transitive Refl Refl = Refl|    (compare the definition of â‰¡-transitive)
-- we could also just write
-- |testproof = Refl|


-- insertSort can of course be formulated more generically:
-- the only additional ingredient we need is a boolean "less equal"
-- test for the type |A|, i.e. a function of type |A â†’ A â†’ ğ”¹|:

insert : {A : Set} â†’ (A â†’ A â†’ ğ”¹) â†’
         A â†’ List A â†’ List A
insert le a [] = a :: []
insert le a (b :: bs) with le a b
...| true  = a :: b :: bs
...| false = b :: insert le a bs

insertSort : {A : Set} â†’ ( A â†’ A â†’ ğ”¹) â†’
             List A â†’ List A
insertSort le  []       = []
insertSort le (a :: as) = insert le a (insertSort le as)

----------------------------------------- 
-- Properties of insertSort (and other
-- "sorting functions")
-----------------------------------------

-- For any type |A| and an arbitrary function of type |A â†’ A â†’ ğ”¹| we have
-- defined a function
--  |insertSort {A} le : List A â†’ List A|
-- Of course, if our "less equals" test doesn't have the properties we expect
-- from a "linear order", this function will not really deserve to be called
-- a "sorting": Think about what |insertSort {A} le| would do for |le| set to e.g.,
-- |Î» x y â†’ true|, |Î» x y â†’ false|, or (for |A = ğ”¹|) |Î» x y â†’ x| 

-- So, we will define properties of functions |A â†’ A â†’ ğ”¹| that are
-- reasonable to require of a "less equals" test:

-- Reflexivity: 

IsReflexive : {A : Set} â†’ (A â†’ A â†’ ğ”¹) â†’ Set
IsReflexive {A} le = (a : A) â†’ le a a â‰¡ true

-- Transitivity:

IsTransitive : {A : Set} â†’ (A â†’ A â†’ ğ”¹) â†’ Set
IsTransitive {A} le = (a b c : A) â†’ le a b â‰¡ true â†’ le b c â‰¡ true â†’ le a c â‰¡ true

-- Totality:

IsTotal : {A : Set} â†’ (A â†’ A â†’ ğ”¹) â†’ Set
IsTotal {A} le = {a b : A} â†’ le a b â‰¡ false â†’ le b a â‰¡ true

-- Note that totality implies reflexivity:

IsTotalâ†’IsReflexive : {A : Set} â†’ (le : A â†’ A â†’ ğ”¹) â†’ IsTotal le â†’ IsReflexive le
IsTotalâ†’IsReflexive le tot a with le a a in eq   -- |eq| will be an equality
...| true  = Refl                                -- here, |eq : le a a â‰¡ true|
...| false =                                     -- here, |eq : le a a â‰¡ false|
        false
        â‰¡âŸ¨ â‰¡-symmetric eq âŸ©
        le a a
        â‰¡âŸ¨ tot eq âŸ©
        true
        â–¡

-- Note that in the |le a a â‰¡ false| branch of this proof, the steps can not
-- just be replaced by Refl ! 


-----------------
-- Sorted lists
-----------------

-- Let's define the property of a list to be sorted w.r.t. some
-- "less equals"-Test |le : A â†’ A â†’ ğ”¹| . That is, for any type
-- |A|, any function |le : A â†’ A â†’ ğ”¹|, and any list |as : List A|,
-- we have to define a type whose elements are proofs that the list
-- |as| is sorted.

-- The idea is to define IsSorted inductively: the empty list is trivially
-- sorted, and a nonempty list is sorted when the tail is sorted and
-- the head is ... well, let's call this "ok to put before this tail":
-- If the tail is empty, every element is "ok to put before". Otherwise
-- the new element has to be less than the tails first element:

data OkAsHead {A : Set} (le : A â†’ A â†’ ğ”¹ ) : A â†’ List A â†’ Set where
  OkAsHeadEmpty : (a : A) â†’ OkAsHead le a []
  OkAsHeadNonEmpty : (a b : A) â†’ (bs : List A) â†’
                     le a b â‰¡ true â†’ OkAsHead le a (b :: bs)

data IsSorted {A : Set} (le : A â†’ A â†’ ğ”¹) : List A â†’ Set where
  IsSortedEmpty : IsSorted le []
  IsSortedNonEmpty : (a : A) â†’ (bs : List A) â†’
                     OkAsHead le a bs â†’ IsSorted le bs â†’ IsSorted le (a :: bs)

-- We want to prove that |insertSort| produces sorted lists. The rough idea
-- is of course to do it by induction: the empty list is sorted, and if we
-- have a sorted list, inserting an element into it with |insert| (this is
-- what |insertSort| does), will yield a sorted list. So the heart of the
-- matter is to prove that |insert a bs| will be sorted whenever |bs| is
-- (this will be |insertSorted| below). As it turns out, it is useful to
-- first prove |okAsHeadLemma| which says that for elements |a b : A| and
-- a list |bs : List| such that |b| is ok as a head for |bs| and |le b a â‰¡ true|,
-- |b| will also be ok as a head for |insert a bs| ... after all, the head of
-- |insert a bs| is either |a| or the head of |bs|. 

okAsHeadLemma : {A : Set} â†’ (le : A â†’ A â†’ ğ”¹) â†’ 
                (a b : A) â†’ (bs : List A) â†’ OkAsHead le b bs â†’ le b a â‰¡ true  â†’
                OkAsHead le b (insert le a bs)
okAsHeadLemma le a b [] bOk leba = OkAsHeadNonEmpty b a [] leba
okAsHeadLemma le a b (b' :: bs') bOk leba with le a b'
... | true  = OkAsHeadNonEmpty b a (b' :: bs') leba
... | false with bOk
...   | (OkAsHeadNonEmpty _ _ _ lebb') = OkAsHeadNonEmpty b b' (insert le a bs') lebb'

-- Now we can prove |insertSorted|. Note the use of |tot|, the totality assumption
-- on |le|: in the case where the argument list is nonempty, i.e. matches the pattern
-- |b :: bs|, and |le a b â‰¡ false| (witnessed by |eq|), we need |tot| to get a proof
-- of |le b a â‰¡ true| that we can use in the call to |okAsHeadLemma|.

insertSorted : {A : Set} â†’ (le : A â†’ A â†’ ğ”¹ ) â†’ IsTotal le â†’
               (a : A) â†’ (bs : List A) â†’
               IsSorted le bs â†’ IsSorted le (insert le a bs)
insertSorted le tot a [] IsSortedEmpty = IsSortedNonEmpty a [] (OkAsHeadEmpty a) IsSortedEmpty
insertSorted le tot a (b :: bs) (IsSortedNonEmpty b bs bOk bsSorted) with le a b in eq
... | true  = let
                okAsHeadabbs : OkAsHead le a (b :: bs)
                okAsHeadabbs = OkAsHeadNonEmpty a b bs eq
                bbsSorted : IsSorted le (b :: bs)
                bbsSorted = (IsSortedNonEmpty b bs bOk bsSorted)
              in
                IsSortedNonEmpty a (b :: bs) okAsHeadabbs bbsSorted
... | false = let
                bOkinsabs : OkAsHead le b (insert le a bs)
                bOkinsabs = okAsHeadLemma le a b bs bOk (tot eq)
                insabsSorted : IsSorted le (insert le a bs)
                insabsSorted = insertSorted le tot a bs bsSorted
              in
                IsSortedNonEmpty b (insert le a bs) bOkinsabs insabsSorted

-- As we hoped, insertSortSorted now is a simple induction on the input list:

insertSortSorted : {A : Set} â†’ (le : A â†’ A â†’ ğ”¹) â†’ IsTotal le â†’
                (as : List A) â†’ IsSorted le (insertSort le as)
insertSortSorted le tot [] = IsSortedEmpty
insertSortSorted le tot (a :: as) = 
              let
                asSorted = insertSort le as
                IH : IsSorted le (insertSort le as)
                IH = insertSortSorted le tot as
              in
                insertSorted le tot a asSorted IH

